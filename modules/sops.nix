{ inputs
, config
, lib
, ...
}:
let
  flakeConfig = config;
  user = flakeConfig.flake.meta.user.username;

  # Secret file paths
  commonFile = ../secrets/common.yaml;
  apisFile = ../secrets/apis.yaml;
  containersFile = ../secrets/containers.yaml;
  developmentFile = ../secrets/development.yaml;

  # Permission presets
  rootOnly = {
    mode = "0400";
  };
  wheelReadable = {
    mode = "0440";
    group = "wheel";
  };
  worldReadable = {
    mode = "0444";
  };
  userOwned = {
    mode = "0600";
    owner = user;
  };
  userReadOnly = {
    mode = "0400";
    owner = user;
  };

  # Bulk secret generators
  mkSecrets =
    file: names:
    lib.genAttrs names (_: {
      sopsFile = file;
    });

  mkSecretsWithOpts =
    file: opts: names:
    lib.genAttrs names (_: lib.recursiveUpdate { sopsFile = file; } opts);

  # Single secret with full control
  mkSecret = file: opts: lib.recursiveUpdate { sopsFile = file; } opts;
in
{
  flake.modules = {
    homeManager = {
      security =
        { pkgs, ... }:
        {
          home.packages = with pkgs; [
            sops
            age
          ];
        };

      # Home Manager SOPS module - works on both NixOS and Darwin
      secrets =
        { config
        , lib
        , ...
        }:
        let
          hmConfig = config;
        in
        {
          imports = [ inputs.sops-nix.homeManagerModules.sops ];

          sops = {
            defaultSopsFile = commonFile;
            age.keyFile = "${hmConfig.home.homeDirectory}/.config/sops/age/keys.txt";
            age.generateKey = true;

            secrets = lib.mkMerge [
              # Common secrets (no special options needed for HM)
              (mkSecrets commonFile [
                "github-token"
                "smb-username"
                "smb-password"
              ])

              # API secrets
              (mkSecrets apisFile [
                "context7-api-key"
                "kagi-api-token"
                "openai-api-key"
              ])

              # Special cases with custom options
              {
                ssh-private-key = mkSecret commonFile {
                  path = "${hmConfig.home.homeDirectory}/.ssh/id_ed25519";
                };
              }
            ];
          };

          # Use activation script to generate Fish secrets file after SOPS decryption
          home.activation.generateFishSecrets =
            let
              secretsMap = {
                CONTEXT7_API_KEY = hmConfig.sops.secrets.context7-api-key.path;
                GITHUB_TOKEN = hmConfig.sops.secrets.github-token.path;
                COPILOT_TOKEN = hmConfig.sops.secrets.github-token.path;
                KAGI_API_TOKEN = hmConfig.sops.secrets.kagi-api-token.path;
                OPENAI_API_KEY = hmConfig.sops.secrets.openai-api-key.path;
              };

              checkSecrets = lib.concatStringsSep "\n" (
                lib.mapAttrsToList
                  (name: path: ''
                    for i in $(seq 1 50); do
                      if [ -r ${path} ]; then
                        break
                      fi

                      if [ "$i" -eq 50 ]; then
                        echo "Error: missing or unreadable secret ${name} at ${path}" >&2
                        exit 1
                      fi

                      sleep 0.2
                    done
                  '')
                  secretsMap
              );

              readSecrets = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: path: "${name}=$(cat ${path})") secretsMap
              );

              fishExports = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: _: "set -gx ${name} '$" + name + "'") secretsMap
              );
            in
            lib.hm.dag.entryAfter [ "sops-nix" "sopsInstallSecrets" "writeBoundary" ] ''
                            $DRY_RUN_CMD mkdir -p ${hmConfig.home.homeDirectory}/.config/fish

                            ${checkSecrets}
                 
                            ${readSecrets}
                
                            cat > ${hmConfig.home.homeDirectory}/.config/fish/private.fish << EOF
              # Auto-generated by Home Manager - secrets loaded at activation time
              ${fishExports}
              EOF
            '';
        };
    };

    # NixOS-specific SOPS module (system-level secrets)
    nixos.secrets =
      { config
      , ...
      }:
      let
        nixosConfig = config;
      in
      {
        imports = [ inputs.sops-nix.nixosModules.sops ];

        sops = {
          defaultSopsFile = commonFile;
          age.keyFile = "/var/lib/sops-nix/key.txt";
          age.generateKey = true;

          secrets = lib.mkMerge [
            # Common secrets - root only
            (mkSecretsWithOpts commonFile rootOnly [
              "smb-username"
              "smb-password"
            ])

            # Common secrets - wheel readable
            (mkSecretsWithOpts commonFile wheelReadable [
              "github-token"
            ])

            # API secrets - wheel readable
            (mkSecretsWithOpts apisFile wheelReadable [
              "kagi-api-token"
              "openai-api-key"
              "wakapi-api-key"
              "wakapi-password-salt"
            ])

            # API secrets - world readable
            (mkSecretsWithOpts apisFile worldReadable [
              "context7-api-key"
            ])

            # Container secrets - root only
            (mkSecretsWithOpts containersFile rootOnly [
              "pihole-default-password"
              "rpi-pihole-password-token"
              "synology-api-username"
              "synology-api-password"
              "mullvad-wireguard-private-key"
              "mullvad-wireguard-addresses"
              "gluetun-control-api-key"
              "linkwarden-postgres-password"
              "linkwarden-nextauth-secret"
              "linkwarden-meili-master-key"
              "linkwarden-access-token"
              "tailscale-api-key"
              "nextdns-profile-id"
              "nextdns-api-key"
              "speedtest-tracker-app-key"
              "speedtest-tracker-api-token"
            ])

            # Container secrets - wheel readable
            (mkSecretsWithOpts containersFile wheelReadable [
              "komodo-web-api-key"
              "komodo-web-api-secret"
              "portainer-api-key"
              "ha-access-token"
            ])

            # Development secrets - user owned
            (mkSecretsWithOpts developmentFile userReadOnly [
              "npm-personal-access-token"
            ])

            # Special cases
            {
              ssh-private-key = mkSecret commonFile userOwned;
            }
          ];

          templates = {
            "smbcredentials" = {
              content = ''
                username=${nixosConfig.sops.placeholder.smb-username}
                password=${nixosConfig.sops.placeholder.smb-password}
              '';
              mode = "0600";
              owner = user;
            };

            "nix-github-token" = {
              content = ''
                access-tokens = github.com=${nixosConfig.sops.placeholder.github-token}
              '';
              mode = "0440";
              group = "wheel";
            };

          };
        };

        nix.extraOptions = ''
          !include ${nixosConfig.sops.templates."nix-github-token".path}
        '';
      };

    # Darwin-specific SOPS module (system-level secrets)
    darwin.secrets =
      { config
      , ...
      }:
      let
        darwinConfig = config;
      in
      {
        imports = [ inputs.sops-nix.darwinModules.sops ];

        sops = {
          defaultSopsFile = commonFile;
          age.keyFile = "/var/lib/sops-nix/key.txt";
          age.generateKey = true;

          secrets = lib.mkMerge [
            # Common secrets - root only
            (mkSecretsWithOpts commonFile rootOnly [
              "smb-username"
              "smb-password"
            ])

            # Common secrets - wheel readable
            (mkSecretsWithOpts commonFile wheelReadable [
              "github-token"
            ])

            # API secrets - wheel readable
            (mkSecretsWithOpts apisFile wheelReadable [
              "kagi-api-token"
              "openai-api-key"
              "wakapi-api-key"
              "wakapi-password-salt"
            ])

            # API secrets - world readable
            (mkSecretsWithOpts apisFile worldReadable [
              "context7-api-key"
            ])

            # Development secrets - user owned
            (mkSecretsWithOpts developmentFile userReadOnly [
              "npm-personal-access-token"
            ])

            # Special cases
            {
              ssh-private-key = mkSecret commonFile userOwned;
            }
          ];

          templates = {
            "smbcredentials" = {
              content = ''
                username=${darwinConfig.sops.placeholder.smb-username}
                password=${darwinConfig.sops.placeholder.smb-password}
              '';
              mode = "0600";
              owner = user;
            };

            "nix-github-token" = {
              content = ''
                access-tokens = github.com=${darwinConfig.sops.placeholder.github-token}
              '';
              mode = "0440";
              group = "wheel";
            };
          };
        };

        nix.extraOptions = ''
          !include ${darwinConfig.sops.templates."nix-github-token".path}
        '';
      };
  };
}
