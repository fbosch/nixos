{ inputs
, config
, lib
, ...
}:
let
  flakeConfig = config;
  commonFile = ../secrets/common.yaml;
  apisFile = ../secrets/apis.yaml;
  containersFile = ../secrets/containers.yaml;
  developmentFile = ../secrets/development.yaml;
  mkSecret =
    file: extra:
    lib.mkMerge [
      { sopsFile = file; }
      extra
    ];
in
{
  flake.modules = {
    homeManager = {
      security =
        { pkgs, ... }:
        {
          home.packages = with pkgs; [
            sops
            age
          ];
        };

      # Home Manager SOPS module - works on both NixOS and Darwin
      secrets =
        { config
        , lib
        , ...
        }:
        let
          hmConfig = config;
        in
        {
          imports = [ inputs.sops-nix.homeManagerModules.sops ];

          sops = {
            defaultSopsFile = commonFile;
            age.keyFile = "${hmConfig.home.homeDirectory}/.config/sops/age/keys.txt";
            # Auto-generate age key on first activation
            age.generateKey = true;

            secrets = {
              github-token = mkSecret commonFile { };
              smb-username = mkSecret commonFile { };
              smb-password = mkSecret commonFile { };
              context7-api-key = mkSecret apisFile { };
              kagi-api-token = mkSecret apisFile { };
              openai-api-key = mkSecret apisFile { };
              ssh-private-key = mkSecret commonFile {
                path = "${hmConfig.home.homeDirectory}/.ssh/id_ed25519";
              };
            };

          };

          # Use activation script to generate Fish secrets file after SOPS decryption
          home.activation.generateFishSecrets =
            let
              # Map environment variable names to secret paths
              secretsMap = {
                CONTEXT7_API_KEY = hmConfig.sops.secrets.context7-api-key.path;
                KAGI_API_TOKEN = hmConfig.sops.secrets.kagi-api-token.path;
                OPENAI_API_KEY = hmConfig.sops.secrets.openai-api-key.path;
              };

              # Generate script to read each secret into a variable
              readSecrets = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: path: ''${name}=$(cat ${path} 2>/dev/null || echo "")'') secretsMap
              );

              # Generate Fish set commands with bash variable expansion
              fishExports = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: _: "set -gx ${name} '$" + name + "'") secretsMap
              );
            in
            lib.hm.dag.entryAfter [ "sopsInstallSecrets" "writeBoundary" ] ''
                            $DRY_RUN_CMD mkdir -p ${hmConfig.home.homeDirectory}/.config/fish
                
                            ${readSecrets}
                
                            cat > ${hmConfig.home.homeDirectory}/.config/fish/private.fish << EOF
              # Auto-generated by Home Manager - secrets loaded at activation time
              ${fishExports}
              EOF
            '';

        };
    };

    # NixOS-specific SOPS module (system-level secrets)
    nixos.secrets =
      { config
      , ...
      }:
      let
        nixosConfig = config;
      in
      {
        imports = [ inputs.sops-nix.nixosModules.sops ];

        sops = {
          defaultSopsFile = commonFile;
          age.keyFile = "/var/lib/sops-nix/key.txt";
          # This will generate a new key if the key specified above does not exist
          age.generateKey = true;

          secrets = {
            github-token = mkSecret commonFile {
              mode = "0440";
              group = "wheel";
            };
            pihole-default-password = mkSecret containersFile {
              mode = "0400";
            };
            rpi-pihole-password-token = mkSecret containersFile {
              mode = "0400";
            };
            smb-username = mkSecret commonFile {
              mode = "0400";
            };
            smb-password = mkSecret commonFile {
              mode = "0400";
            };
            synology-api-username = mkSecret containersFile {
              mode = "0400";
            };
            synology-api-password = mkSecret containersFile {
              mode = "0400";
            };
            context7-api-key = mkSecret apisFile {
              mode = "0444";
            };
            kagi-api-token = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            openai-api-key = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            npm-personal-access-token = mkSecret developmentFile {
              mode = "0400";
              owner = flakeConfig.flake.meta.user.username;
            };
            wakapi-api-key = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            wakapi-password-salt = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            ssh-private-key = mkSecret commonFile {
              mode = "0600";
              owner = flakeConfig.flake.meta.user.username;
            };
            komodo-web-api-key = mkSecret containersFile {
              mode = "0440";
              group = "wheel";
            };
            komodo-web-api-secret = mkSecret containersFile {
              mode = "0440";
              group = "wheel";
            };
            portainer-api-key = mkSecret containersFile {
              mode = "0440";
              group = "wheel";
            };
            ha-access-token = mkSecret containersFile {
              mode = "0440";
              group = "wheel";
            };
            linkwarden-postgres-password = mkSecret containersFile {
              mode = "0400";
            };
            linkwarden-nextauth-secret = mkSecret containersFile {
              mode = "0400";
            };
            linkwarden-meili-master-key = mkSecret containersFile {
              mode = "0400";
            };
            linkwarden-access-token = mkSecret containersFile {
              mode = "0400";
            };
            tailscale-api-key = mkSecret containersFile {
              mode = "0400";
            };
          };

          templates = {
            # Generate .smbcredentials file from SOPS secrets
            "smbcredentials" = {
              content = ''
                username=${nixosConfig.sops.placeholder.smb-username}
                password=${nixosConfig.sops.placeholder.smb-password}
              '';
              mode = "0600";
              owner = flakeConfig.flake.meta.user.username;
            };

            "pihole-webpassword" = {
              content = ''
                FTLCONF_webserver_api_password=${nixosConfig.sops.placeholder.pihole-default-password}
              '';
              mode = "0400";
            };

            "glance-env" = {
              content = ''
                KOMODO_URL=https://komodo.corvus-corax.synology.me
                KOMODO_API_KEY=${nixosConfig.sops.placeholder.komodo-web-api-key}
                KOMODO_API_SECRET=${nixosConfig.sops.placeholder.komodo-web-api-secret}
                PORTAINER_URL=https://portainer.corvus-corax.synology.me
                PORTAINER_API_KEY=${nixosConfig.sops.placeholder.portainer-api-key}
                SYNOLOGY_URL=https://corvus-corax.synology.me
                SYNOLOGY_USERNAME=${nixosConfig.sops.placeholder.synology-api-username}
                SYNOLOGY_PASSWORD=${nixosConfig.sops.placeholder.synology-api-password}
                HASS_URL=https://ha.corvus-corax.synology.me
                HASS_API_KEY=${nixosConfig.sops.placeholder.ha-access-token}
                LINKWARDEN_TOKEN=${nixosConfig.sops.placeholder.linkwarden-access-token}
                PIHOLE_PASSWORD=${nixosConfig.sops.placeholder.rpi-pihole-password-token}
                GITHUB_TOKEN=${nixosConfig.sops.placeholder.github-token}
                TAILSCALE_API_KEY=${nixosConfig.sops.placeholder.tailscale-api-key}
              '';
              mode = "0400";
            };

            # Generate nix.conf snippet with GitHub token
            "nix-github-token" = {
              content = ''
                access-tokens = github.com=${nixosConfig.sops.placeholder.github-token}
              '';
              mode = "0440";
              group = "wheel";
            };

            # Linkwarden environment file with secrets
            "linkwarden-env" = {
              content = ''
                POSTGRES_PASSWORD=${nixosConfig.sops.placeholder.linkwarden-postgres-password}
                NEXTAUTH_SECRET=${nixosConfig.sops.placeholder.linkwarden-nextauth-secret}
                MEILI_MASTER_KEY=${nixosConfig.sops.placeholder.linkwarden-meili-master-key}
                DISABLE_PRESERVATION=true
              '';
              mode = "0400";
            };
          };
        };

        nix.extraOptions = ''
          !include ${nixosConfig.sops.templates."nix-github-token".path}
        '';
      };

    # Darwin-specific SOPS module (system-level secrets)
    darwin.secrets =
      { config
      , ...
      }:
      let
        darwinConfig = config;
      in
      {
        imports = [ inputs.sops-nix.darwinModules.sops ];

        sops = {
          defaultSopsFile = commonFile;
          age.keyFile = "/var/lib/sops-nix/key.txt";
          # This will generate a new key if the key specified above does not exist
          age.generateKey = true;

          secrets = {
            github-token = mkSecret commonFile {
              mode = "0440";
              group = "wheel";
            };
            smb-username = mkSecret commonFile {
              mode = "0400";
            };
            smb-password = mkSecret commonFile {
              mode = "0400";
            };
            context7-api-key = mkSecret apisFile {
              mode = "0444";
            };
            kagi-api-token = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            openai-api-key = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            npm-personal-access-token = mkSecret developmentFile {
              mode = "0400";
              owner = flakeConfig.flake.meta.user.username;
            };
            wakapi-api-key = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            wakapi-password-salt = mkSecret apisFile {
              mode = "0440";
              group = "wheel";
            };
            ssh-private-key = mkSecret commonFile {
              mode = "0600";
              owner = flakeConfig.flake.meta.user.username;
            };
          };

          templates = {
            # Generate .smbcredentials file from SOPS secrets
            "smbcredentials" = {
              content = ''
                username=${darwinConfig.sops.placeholder.smb-username}
                password=${darwinConfig.sops.placeholder.smb-password}
              '';
              mode = "0600";
              owner = flakeConfig.flake.meta.user.username;
            };

            # Generate nix.conf snippet with GitHub token
            "nix-github-token" = {
              content = ''
                access-tokens = github.com=${darwinConfig.sops.placeholder.github-token}
              '';
              mode = "0440";
              group = "wheel";
            };
          };
        };

        nix.extraOptions = ''
          !include ${darwinConfig.sops.templates."nix-github-token".path}
        '';
      };
  };
}
