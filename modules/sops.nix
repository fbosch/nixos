{ inputs, config, ... }:
let
  flakeConfig = config;
in
{
  flake.modules = {
    homeManager = {
      security =
        { pkgs, ... }:
        {
          home.packages = with pkgs; [
            sops
            age
          ];
        };

      # Home Manager SOPS module - works on both NixOS and Darwin
      secrets =
        { config
        , lib
        , ...
        }:
        let
          hmConfig = config;
        in
        {
          imports = [ inputs.sops-nix.homeManagerModules.sops ];

          sops = {
            defaultSopsFile = ../secrets/secrets.yaml;
            age.keyFile = "${hmConfig.home.homeDirectory}/.config/sops/age/keys.txt";
            # Auto-generate age key on first activation
            age.generateKey = true;

            secrets = {
              github-token = { };
              smb-username = { };
              smb-password = { };
              context7-api-key = { };
              kagi-api-token = { };
              openai-api-key = { };
              ssh-private-key = {
                path = "${hmConfig.home.homeDirectory}/.ssh/id_ed25519";
              };
            };
          };

          # Use activation script to generate Fish secrets file after SOPS decryption
          home.activation.generateFishSecrets =
            let
              # Map environment variable names to secret paths
              secretsMap = {
                CONTEXT7_API_KEY = hmConfig.sops.secrets.context7-api-key.path;
                KAGI_API_TOKEN = hmConfig.sops.secrets.kagi-api-token.path;
                OPENAI_API_KEY = hmConfig.sops.secrets.openai-api-key.path;
              };

              # Generate script to read each secret into a variable
              readSecrets = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: path: ''${name}=$(cat ${path} 2>/dev/null || echo "")'') secretsMap
              );

              # Generate Fish set commands with bash variable expansion
              fishExports = lib.concatStringsSep "\n" (
                lib.mapAttrsToList (name: _: "set -gx ${name} '$" + name + "'") secretsMap
              );
            in
            lib.hm.dag.entryAfter [ "writeBoundary" ] ''
                            $DRY_RUN_CMD mkdir -p ${hmConfig.home.homeDirectory}/.config/fish
                
                            ${readSecrets}
                
                            cat > ${hmConfig.home.homeDirectory}/.config/fish/private.fish << EOF
              # Auto-generated by Home Manager - secrets loaded at activation time
              ${fishExports}
              EOF
            '';

        };
    };

    # NixOS-specific SOPS module (system-level secrets)
    nixos.secrets =
      { config
      , ...
      }:
      let
        nixosConfig = config;
      in
      {
        imports = [ inputs.sops-nix.nixosModules.sops ];

        sops = {
          defaultSopsFile = ../secrets/secrets.yaml;
          age.keyFile = "/var/lib/sops-nix/key.txt";
          # This will generate a new key if the key specified above does not exist
          age.generateKey = true;

          secrets = {
            github-token = {
              mode = "0440";
              group = "wheel";
            };
            pihole-password = {
              mode = "0400";
            };
            smb-username = {
              mode = "0400";
            };
            smb-password = {
              mode = "0400";
            };
            context7-api-key = {
              mode = "0444";
            };
            kagi-api-token = {
              mode = "0440";
              group = "wheel";
            };
            openai-api-key = {
              mode = "0440";
              group = "wheel";
            };
            ssh-private-key = {
              mode = "0600";
              owner = flakeConfig.flake.meta.user.username;
            };
          };

          # Generate .smbcredentials file from SOPS secrets
          templates."smbcredentials" = {
            content = ''
              username=${nixosConfig.sops.placeholder.smb-username}
              password=${nixosConfig.sops.placeholder.smb-password}
            '';
            mode = "0600";
            owner = flakeConfig.flake.meta.user.username;
          };

          templates."pihole-webpassword" = {
            content = ''
              WEBPASSWORD=${nixosConfig.sops.placeholder.pihole-password}
            '';
            mode = "0400";
          };

          # Generate nix.conf snippet with GitHub token
          templates."nix-github-token" = {
            content = ''
              access-tokens = github.com=${nixosConfig.sops.placeholder.github-token}
            '';
            mode = "0440";
            group = "wheel";
          };
        };

        nix.extraOptions = ''
          !include ${nixosConfig.sops.templates."nix-github-token".path}
        '';
      };
  };
}
