{
  flake.modules.nixos.shell =
    { pkgs, ... }:
    {
      environment.shells = [
        pkgs.fish
        pkgs.dash
      ];
    };

  flake.modules.homeManager.shell =
    { config
    , lib
    , pkgs
    , ...
    }:
    let
      requiredSecrets = [
        "context7-api-key"
        "github-token"
        "kagi-api-token"
        "openai-api-key"
      ];
      hasRequiredSecrets = lib.all
        (
          name: lib.hasAttrByPath [ "sops" "secrets" name "path" ] config
        )
        requiredSecrets;
      secretsMap = {
        CONTEXT7_API_KEY = config.sops.secrets.context7-api-key.path;
        GITHUB_TOKEN = config.sops.secrets.github-token.path;
        COPILOT_TOKEN = config.sops.secrets.github-token.path;
        KAGI_API_TOKEN = config.sops.secrets.kagi-api-token.path;
        OPENAI_API_KEY = config.sops.secrets.openai-api-key.path;
      };

      checkSecrets = lib.concatStringsSep "\n" (
        lib.mapAttrsToList
          (name: path: ''
            for i in $(seq 1 50); do
              if [ -r ${path} ]; then
                break
              fi

              if [ "$i" -eq 50 ]; then
                echo "Error: missing or unreadable secret ${name} at ${path}" >&2
                exit 1
              fi

              sleep 0.2
            done
          '')
          secretsMap
      );

      readSecrets = lib.concatStringsSep "\n" (
        lib.mapAttrsToList (name: path: "${name}=$(cat ${path})") secretsMap
      );

      fishExports = lib.concatStringsSep "\n" (
        lib.mapAttrsToList (name: _: "set -gx ${name} '$" + name + "'") secretsMap
      );
    in
    {
      home.packages = with pkgs; [
        fish
        zsh
        dash
        starship
        zoxide
        atuin
      ];

      home.activation.generateFishSecrets = lib.mkIf hasRequiredSecrets (
        lib.hm.dag.entryAfter [ "sops-nix" "sopsInstallSecrets" "writeBoundary" ] ''
          $DRY_RUN_CMD mkdir -p ${config.home.homeDirectory}/.config/fish

          ${checkSecrets}

          ${readSecrets}

          cat > ${config.home.homeDirectory}/.config/fish/private.fish << EOF
          # Auto-generated by Home Manager - secrets loaded at activation time
          ${fishExports}
          EOF
        ''
      );
    };
}
